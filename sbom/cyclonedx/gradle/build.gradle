plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.cyclonedx.bom' version '1.8.2'
}

group = 'com.thomasvitale'
version = '1.0'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.test {
    useJUnitPlatform()
}

cyclonedxBom {
    projectType = "application"
    includeConfigs = ["runtimeClasspath"]
    outputFormat = "json"
    schemaVersion = "1.5"
}
tasks.bootJar.finalizedBy 'cyclonedxBom'

tasks.bootBuildImage {
    if (System.getProperty( "os.arch" ).toLowerCase().startsWith('aarch')) {
        // This builder is only for ARM64 systems.
        // For more info: https://github.com/dashaun/paketo-arm64
        builder = "docker.io/dashaun/builder:tiny"
    } else {
        builder = "docker.io/paketobuildpacks/builder-jammy-tiny"
    }
}
