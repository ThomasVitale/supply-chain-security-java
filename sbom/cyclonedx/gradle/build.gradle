plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.graalvm.buildtools.native' version '0.9.27'
    id 'org.cyclonedx.bom' version '1.8.1'
}

group = 'com.thomasvitale'
version = '1.0'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyLocking {
    lockAllConfigurations()
}

tasks.test {
    useJUnitPlatform()
}

cyclonedxBom {
    projectType = "application"
    skipConfigs = ["testCompileClasspath"]
    outputFormat = "json"
    schemaVersion = "1.4"
}
tasks.assemble.finalizedBy 'cyclonedxBom'

graalvmNative {
    binaries {
        configureEach {
            buildArgs.add("--enable-sbom=cyclonedx,export")
        }
    }
}
