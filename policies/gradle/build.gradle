plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.cyclonedx.bom' version '1.7.4'
}

group = 'com.thomasvitale'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyLocking {
    lockAllConfigurations()
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.cyclonedxBom {
    projectType = "application"
    //skipConfigs = ["testCompileClasspath"]
    outputFormat = "json"
}
tasks.assemble.finalizedBy 'cyclonedxBom'

tasks.bootBuildImage {
    if (System.getProperty( "os.arch" ).toLowerCase().startsWith('aarch')) {
        // This builder is only for ARM64 systems.
        // For more info: https://github.com/dashaun/paketo-arm64
        builder = "docker.io/dashaun/builder:tiny"
    } else {
        builder = "docker.io/paketobuildpacks/builder-jammy-tiny"
    }

    imageName = "demo"
}
